/*

This is the growing standard library of PISC.

*/

/* t and f respectively push true and false onto the stack */
: t ( -- t ) "t" extern-call ;
: f ( -- t ) "f" extern-call ;

/* stack-empty? checks if there are any entries on the stack
: stack-empty? ( .. -- empty? ) primitive ;
*/

/* The following calls are the current stack-shuffling primitives */

/* pick-dup duplicates the nth item back on the stack */
: pick-dup ( .. n -- .. item ) "pick-dup" extern-call ;

/* pick-drop removes the item from the nth stack slot, placing it atop the stack */
: pick-drop ( .. n -- ..` item ) "pick-drop" extern-call ;

/* pick-del deletes the nth stack entry back, removing it from the stack entirely */
: pick-del ( .. n --  ..`  ) "pick-del" extern-call ;


/* Len is the go defined word that defers length checking to the go type system */
: len ( lenable -- length ) "len" extern-call ;

/* import current takes a file name, and attempts to execute the file at that relative file path. 

NOTE: Currently this word is actually defined inside the go executable
*/

: import ( filepath -- .. ) "import" extern-call ;

"loops.pisc" import
"bools.pisc" import 
"strings.pisc" import
"locals.pisc" import
"io.pisc" import
"dicts.pisc" import
"vectors.pisc" import
"symbols.pisc" import
"math.pisc" import




/* Local Prefixes */
/* # is the import prefix */
:PRE : ( val name --  ) set-local ;
:PRE $ ( name -- val ) get-local ;

/* Stack shufflers */
: dup ( a -- a a ) 0 pick-dup ;
: 2dup ( a b -- a b a b ) 2 [ 1 pick-dup ] times ;
: swap ( a b -- b a ) 1 pick-drop ;
: drop ( a -- ) 0 pick-del ;
: nip ( a b -- b ) 1 pick-del ;
: 2drop ( a b -- ) 2 [ drop ] times ;
: 3drop ( a b c -- ) 3 [ drop ] times ;
: over ( x y -- x y x ) 1 pick-dup ;
/* Used locals for fun and profit 
: dip ( a quot -- a ) swap :a call $a ;
*/
: dip ( a quot -- a ) "dip" extern-call ; 
: keep ( ..a x quot: [ ..a x --- ..b ] -- ..b x ) over [ call ] dip ;
: bi ( a quot1 quot2 -- ... ) [ keep ] dip call ;

/*
: change ( quot name -- ... ) drop-locals :name :quot  ;

: change ( quot varName -- .. ) drop-locals :name :quot $name get-local $quot call $name set-local get-locals ;
*/
/* Copied from factor */
: change ( quot varName -- .. ) swap [ [ get-local ] keep ] dip dip set-local ;
:PRE $: ( quot varName -- ) change ;

: str-neq ( str-a str-b -- eq? ) str-eq not ;


/* Symbol manipulation */
: is-symbol ( symb symb  -- eq? ) symb-neq not ;

/* Arrays */

/* Take all the elements placed on the stack by quot and put in an array */
: quot>vector ( quot -- vec )
	<vector> :vec
	<symbol> dup :mark /* Mark the stack */
	swap call /* Fill the stack with info from the quotation */
	[ dup $mark symb-neq ] [ $vec swap vec-prepend :vec ] while 
	drop /* the mark */ $vec ;


/* If statement */
: if ( ? true false -- res ) ? call ;
/* When statement for when you don't need all of if */
: when ( ? true -- res ) [ ] ? call ;


/* Some basic math */
: even? ( n -- ? ) 2 mod zero? ;
: divisor? ( n m -- ? ) mod zero? ;

: = ( a b -- ? ) - zero? ;

/* < is already defined */
: >= ( a b -- ? ) < not ;
: <= ( a b -- ? ) 2dup < [ = ] dip or ;
: > ( a b -- ? ) <= not ;

: abs ( a -- a ) dup 0 < [ -1 * ] when ; 

/* : eq ( a b -- ? ) */
: . ( a -- ) drop ;

: print ( a -- ) >string priv_puts ;

: inspect ( a -- a ) dup print ;

