/*

This is the growing standard library of PISC.

*/

/* Local Prefixes */
:PRE : ( val name --  ) set-local ;
:PRE $ ( name -- val ) get-local ;
:PRE % ( name -- .. ) get-local call ;

/* Stack shufflers */
: dup ( a -- a a ) 0 pick-dup ;
: 2dup ( a b -- a b a b ) 2 [ 1 pick-dup ] times ;
: swap ( a b -- b a ) 1 pick-drop ;
: drop ( a -- ) 0 pick-del ;
: 2drop ( a b -- ) 2 [ drop ] times ;
: 3drop ( a b c -- ) 3 [ drop ] times ;
: over ( x y -- x y x ) 1 pick-dup ;
/* Uses locals for fun and profit */
: dip ( a quot -- a ) swap :a call $a ;
: keep ( ..a x -- ..b ) over [ call ] dip ;
: bi ( a quot1 quot2 -- ... ) [ keep ] dip call ;



/* Symbol manipulation */
: is-symbol ( symb symb  -- eq? ) symb-neq not ;

/* Arrays */

/* Take all the elements placed on the stack by quot and put in an array */
: quot>vector ( quot -- vec )
	:quot
	<symbol> dup :mark /* Mark the stack */
	<vector> :vec 
	$quot call /* Fill the stack with info from the quotation */
	[ dup $mark symb-neq ] [ $vec swap vec-prepend :vec ] while 
	drop /* the mark */ $vec ;

: quot>dict ( quot -- dict )
	:quot
	<dict>
	get-locals %quot 
	[ dict-set ] each-local
	drop-locals ;

/* If statement */
: if ( ? true false -- res ) ? call ;
/* When statement for when you don't need all of if */
: when ( ? true -- res ) [ ] ? call ;

/* Some basic math */
: even? ( n -- ? ) 2 mod zero? ;
: divisor? ( n m -- ? ) mod zero? ;

: = ( a b -- ? ) - zero? ;

/* < is already defined */
: >= ( a b -- ? ) < not ;
: <= ( a b -- ? ) 2dup < [ = ] dip or ;
: > ( a b -- ? ) <= not ;

/* : eq ( a b -- ? ) */
: . ( a -- ) drop ;

: print ( a -- ) >string priv_puts ;

: inspect ( a -- a ) dup print ;
