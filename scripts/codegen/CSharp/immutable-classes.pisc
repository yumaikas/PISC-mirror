# Change this function to set properties
: *props* ( -- data )
"
DateTime Time
String Message
" process-props ;

: main ( -- ) 
  [  "LogEntry" *props*  ] :args
  args gen-immutable-class
  args gen-ctor-call
;

: str-first-lower ( string -- String ) 
   :s 
   $s 0 1 str-substr str-lower :l
   $l $s 1 $s len str-substr str-concat
;

: str-first-upper ( string -- String ) 
   :s 
   $s 0 1 str-substr str-upper :u
   $u $s 1 $s len str-substr str-concat
;

: NL ( -- nl ) "\n" ;

: TAB ( -- t ) "&nbsp;&nbsp;&nbsp;&nbsp;" ;

: process-props ( str -- props ) 
    str-trim "\n" str-split [
        ";" "" str-replace " " str-split
        splat :n :t <dict> $n str-first-upper <<-name $t <<-type
    ] vec-map 
;

: gen-ctor ( name exposed-props -- str ) 
	:props :name
	${ 
	    # Gen args
          "public " $name "(" NL 

	    { $props [ :q ${ TAB $q ->type " " $q ->name } ] vec-each } ${ "," NL } str-join
	    ")" NL 
	    "{" NL
	    # Gen assignments
	    $props [ ->name :name TAB "this." $name str-first-lower " = " $name ";" NL ] vec-each 
	    "}" NL
	}
;

: gen-immutable-class ( name exposed-props -- str ) 
    :props :name
	${
    	"class " $name " {" NL
    	# emit public get-only properties
    	$props [ :q $q ->type :t $q ->name :n
			TAB "public " $t " " $n " { get { return " $n str-first-lower "; } } " NL
    	] vec-each 
	    NL

	    # emit ctor
	    $name $props gen-ctor NL   

	    # emit private backing fields
	    $props [ :q $q ->type :t $q ->name :n 
	        TAB "private readonly " $t " " $n str-first-lower ";" NL
	    ] vec-each
	   "}"
    }
;

: gen-ctor-call ( name exposed-props -- str ) 
	:props :name
	${ "new " $name "(" NL
	    { $props [ :q ${ TAB $q ->name ": " } ] vec-each } ${ "," NL } str-join
	    ");"
	}   
;

main
