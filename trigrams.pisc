/* By turning comma into a no-op, use commas for helping space a few wide expressions later */
: , ( -- ) ;

seed-rand-time

: char-any ( c chars -- ? )
	:chars :c
 	f :isAny 
	$chars [ $c str-eq $isAny or :isAny ] each-char
	$isAny
; 

: get-words ( string -- words ) 
	:input <vector> :words
	"" :currWord
	$input [ :c
	 	$c " \t\n\r\"" char-any [ 
	 		/* Add the word if it's had a chance to be longer than 0 */
	 		$currWord len 0 > [ $words $currWord vec-append :words ] when
	 		"" :currWord
	 	] [ 
			$currWord $c concat :currWord 
 		] if
	] each-char
	$words $currWord vec-append :words 
	$words 
;

: get-trigram ( words -- key value ) 
	:words
	${ $words 0 vec-at " " $words 1 vec-at } 
	$words 2 vec-at
;

: add-trigram ( trigrams key value -- ) 
	:value :key :trigrams
	$trigrams $key dict-has-key [
		/* Add the element to the vector */
		$trigrams $key dict-get :old
		$old $value vec-append :new
		/* And then put the vector back into the dictionary */
		$trigrams $new $key dict-set
	] [
	 	$trigrams { $value } $key dict-set 
	] if
;

: trigrams-of-string ( string -- trigrams ) 
	get-words :words
	/* " " str-split :words */
	<dict> :trigrams
	[ $words len 3 >= ] [
		$trigrams 
			$words get-trigram 
		add-trigram
		$words vec-dropfront :words
	] while
	$trigrams
;

: last-pair ( words -- pair ) 
	dup :words len :words-len
	$words-len 1 - :end
	$words-len 2 - :nextToEnd
	${ $words $nextToEnd vec-at , " " , $words $end vec-at } 
;

: dict-rand-key ( dict -- key ) dict-get-rand drop ;
: dict-rand-value ( dict -- value ) dict-get-rand nip ;


: text-of-trigrams ( trigrams -- text ) 
	dup :trigrams dict-rand-key " " str-split :text
	$text last-pair :key
	[ $trigrams $key dict-has-key ] [
	     $text $trigrams $key dict-get choice vec-append :text
	     $text last-pair :key
	] while
	$text " " str-join 
;

: text-from-string ( string -- text ) trigrams-of-string text-of-trigrams ;

"poem.txt" filepath>string :input 
$input text-from-string print